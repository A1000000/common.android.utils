apply plugin: "com.android.library"
apply plugin: "com.github.dcendents.android-maven"

android {

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode commitCount()
        versionName "1.2.0"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources = false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        release {
            minifyEnabled false
            shrinkResources = false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    dexOptions {
        preDexLibraries = false
        jumboMode = false
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        ignoreWarnings true
        disable "InvalidPackage"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'com.github.zbra-solutions:android-linq:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "com.github.orhanobut:hawk:1.23"
    implementation 'com.google.code.gson:gson:2.8.6'

    def butterKnifeVersion = '10.2.3'
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "com.github.bumptech.glide:glide:4.11.0"
    implementation "jp.wasabeef:glide-transformations:2.0.2"
    implementation "jp.co.cyberagent.android.gpuimage:gpuimage-library:1.3.0"
    implementation "com.squareup.okio:okio:2.9.0"

    // network
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"
    def okhttpVersion = '4.9.0'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    // rx
    implementation "io.reactivex:rxandroid:1.2.1"

    // pubnub
    compileOnly 'com.pubnub:pubnub:4.6.5'

    // view pager easier interface
    compileOnly "com.sbrukhanda.fragmentviewpager:fragmentviewpager:1.0.0"
}

// Auto-incrementing commit count based on counting commits to current branch√ò (Build #543)
def commitCount() {
    final def stdout = new ByteArrayOutputStream()
    description = "git rev-list --full-history --all"
    println(description)
    exec {
        commandLine 'git', 'rev-list', '--full-history', '--all'
        standardOutput = stdout
    }
    final String output = stdout.toString();
    final List<String> commits = output.split("\n")
    println("Version Code: " + commits.size())
    return commits.size()
}

// region javadoc

apply from: "https://gist.githubusercontent.com/kibotu/994c9cc65fe623b76b76fedfac74b34b/raw/65ee52482ba65050e015f4b00d8d2fc9172a10e8/javadoc.gradle"

// endregion